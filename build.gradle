buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath Libs.kotlin_gradle_plugin
        classpath Libs.coveralls_gradle_plugin
        classpath Libs.dokka_gradle_plugin
        classpath Libs.com_jfrog_bintray_gradle_plugin
        classpath Libs.openapi_generator_gradle_plugin
        classpath Libs.kotlin_serialization
        classpath Libs.shadow
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version "2.10.1"
    id "com.jfrog.bintray" version "1.8.4"
    id 'de.fayard.buildSrcVersions' version "0.7.0"
}

allprojects {

    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jetbrains.dokka'

    jacoco {
        toolVersion = '0.8.3'
    }

    compileKotlin.kotlinOptions.jvmTarget = "1.8"
    compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    version = project.hasProperty('releaseVersion') ? project.releaseVersion : 'LOCAL'
    group = 'org.http4k'

    test {
        useJUnitPlatform()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

//    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask, overwrite: true, dependsOn: javadoc) {
//        outputDirectory = javadoc.destinationDir
//        outputFormat = "javadoc"
//        processConfigurations = []
//    }
//    task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
//        classifier = 'javadoc'
//        from dokkaJavadoc.outputDirectory
//    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    task testJar(type: Jar) {
        classifier "test"
        from sourceSets.test.output
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }

    artifacts {
        testArtifacts testJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = archivesBaseName

                pom.withXml {
                    asNode().appendNode('name', archivesBaseName)
                    asNode().appendNode('description', description)
                    asNode().appendNode('url', 'https://http4k.org')
                    asNode().appendNode('developers')
                        .appendNode('developer').appendNode('name', 'Ivan Sanchez').parent().appendNode('email', 'ivan@http4k.org')
                        .parent().parent()
                        .appendNode('developer').appendNode('name', 'David Denton').parent().appendNode('email', 'david@http4k.org')
                    asNode().appendNode('scm').
                        appendNode('url', 'git@github.com:http4k/' + archivesBaseName + '.git').parent().
                        appendNode('connection', 'scm:git:git@github.com:http4k/http4k.git').parent().
                        appendNode('developerConnection', 'scm:git:git@github.com:http4k/http4k.git')
                    asNode().appendNode('licenses').appendNode('license').
                        appendNode('name', 'Apache License, Version 2.0').parent().
                        appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.html')
                }
                from components.java

                // replace all runtime dependencies with provided
                pom.withXml {
                    asNode().dependencies.'*'.findAll() {
                        it.scope.text() == 'runtime'
                    }.each() {
                        it.scope*.value = 'provided'
                    }
                }
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')

        publications = ['mavenJava']
        dryRun = false
        publish = !project.name.contains("test-function") && !project.name.contains("integration-test")
        pkg {
            repo = 'maven'
            userOrg = 'http4k'
            name = project.name

            desc = description
            websiteUrl = 'https://github.com/http4k/http4k'
            issueTrackerUrl = 'https://github.com/http4k/http4k/issues'
            vcsUrl = 'https://github.com/http4k/http4k.git'
            licenses = ['Apache-2.0']
            labels = []
            publicDownloadNumbers = true
            version {
                name = project.version
                vcsTag = project.version
                gpg {
                    sign = true
                }
                mavenCentralSync {
                    sync = false
                    user = System.getenv('SONATYPE_USER')
                    password = System.getenv('SONATYPE_KEY')
                    close = '1'
                }
            }
        }
    }


    jacocoTestReport {
        reports {
            xml.enabled = true
        }
    }
}

compileTestKotlin.kotlinOptions {
    jvmTarget = "1.8"
    freeCompilerArgs += "-Xjvm-default=enable"
}

coveralls {
    sourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs).files.absolutePath
    jacocoReportPath = file("${buildDir}/reports/jacoco/test/jacocoRootReport.xml")
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    dependsOn = subprojects.test
    getSourceDirectories().from(subprojects.sourceSets.main.allSource.srcDirs)
    getClassDirectories().from(subprojects.sourceSets.main.output)
    getExecutionData().from(subprojects
        .findAll { it.name != 'http4k-bom' && !it.name.contains("integration-test") }
        .jacocoTestReport.executionData)

    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
        xml.destination = file("${buildDir}/reports/jacoco/test/jacocoRootReport.xml")
    }
}

dependencies {
    compile project(":http4k-core")
    compile project(":http4k-aws")
    compile project(":http4k-client-apache")
    compile project(":http4k-client-apache4")
    compile project(":http4k-client-apache-async")
    compile project(":http4k-client-apache4-async")
    compile project(":http4k-client-jetty")
    compile project(":http4k-client-okhttp")
    compile project(":http4k-client-websocket")
    compile project(":http4k-cloudnative")
    compile project(":http4k-contract")
    compile project(":http4k-format-argo")
    compile project(":http4k-format-gson")
    compile project(":http4k-format-jackson")
    compile project(":http4k-format-jackson-xml")
    compile project(":http4k-format-moshi")
    compile project(":http4k-format-xml")
    compile project(":http4k-incubator")
    compile project(":http4k-jsonrpc")
    compile project(":http4k-metrics-micrometer")
    compile project(":http4k-multipart")
    compile project(":http4k-resilience4j")
    compile project(":http4k-security-oauth")
    compile project(":http4k-server-apache")
    compile project(":http4k-server-apache4")
    compile project(":http4k-server-jetty")
    compile project(":http4k-server-ktorcio")
    compile project(":http4k-server-ktornetty")
    compile project(":http4k-server-netty")
    compile project(":http4k-server-ratpack")
    compile project(":http4k-server-undertow")
    compile project(":http4k-serverless-alibaba")
    compile project(":http4k-serverless-gcf")
    compile project(":http4k-serverless-lambda")
    compile project(":http4k-serverless-openwhisk")
    compile project(":http4k-serverless-tencent")
    compile project(":http4k-template-dust")
    compile project(":http4k-template-freemarker")
    compile project(":http4k-template-handlebars")
    compile project(":http4k-template-pebble")
    compile project(":http4k-template-thymeleaf")
    compile project(":http4k-template-dust")
    compile project(":http4k-template-jade4j")
    compile project(":http4k-testing-approval")
    compile project(":http4k-testing-chaos")
    compile project(":http4k-testing-hamkrest")
    compile project(":http4k-testing-kotest")
    compile project(":http4k-testing-servirtium")
    compile project(":http4k-testing-webdriver")

    testCompile Libs.s3
    testCompile Config.TestDependencies
    testCompile project(path: ":http4k-testing-servirtium", configuration: 'testArtifacts')
    testCompile project(path: ":http4k-serverless-gcf", configuration: 'testArtifacts')
    testCompile project(path: ":http4k-serverless-openwhisk", configuration: 'testArtifacts')
}

sourceSets {
    test {
        kotlin {
            srcDirs 'src/test/kotlin'
            srcDirs 'src/docs'
        }
        resources {
            srcDir 'src/docs'
        }
    }
}

dokka {
    outputFormat = "gfm"
    outputDirectory = "$buildDir/ddoc"

    subProjects = subprojects.collect { i -> i.name }

    configuration {
        includes = ['src/packages.md']
        moduleName = "$rootProject.name"
        jdkVersion = 9
    }
}

buildSrcVersions {
    indent = "    "
}

// To update Gradle, run `$ ./gradlew :buildSrcVersions && ./gradlew wrapper`
wrapper {
    gradleVersion = Versions.gradleLatestVersion
    distributionType = Wrapper.DistributionType.ALL
}
